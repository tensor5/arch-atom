# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=atom
pkgver=1.8.0
pkgrel=4
pkgdesc='A hackable text editor for the 21st Century'
arch=('i686' 'x86_64')
url='https://github.com/atom/atom'
license=('MIT' 'custom')
depends=('electron'
         'nodejs-atom-package-manager')
makedepends=('git' 'npm')
conflicts=('atom-editor' 'atom-editor-bin' 'atom-editor-git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/atom/atom/archive/v${pkgver}.tar.gz"
        'beforeunload.patch'
        'deprecated-api.patch'
        'fix-atom-sh.patch'
        'fix-license-path.patch'
        'fix-marker-index.patch'
        'fix-oniguruma.patch'
        'run-as-node.patch'
        'stop-using-nodegit.patch'
        'tree-view.patch'
        'use-system-apm.patch'
        'use-system-electron.patch')
sha256sums=('2950820b2c7ab658135e9cb7c003ff2074ec0a38ac3b324d85cc20bcb237e61f'
            'e92e23bbf839bec6611b2ac76c1f5bba35b476983b0faa9b310288e2956247a2'
            '6fca91b3e80248a96fc4b6b0228602d4dd68ef851cb059a97a7379e72e53b432'
            'd3eb239f53feb3181fb4d57d2db391d7b31ce2188c5bd5b79d9ed3133351cd90'
            '061493b9e53722e194d688a7db9e26d7fbb9a1e2cb86284aae4d7dc61e425400'
            '2569702bee0508ee38a181f28bc99d6844725de206362a010705d84da09735c3'
            'c0ae6c64802bc20219bc9142bd6e62e65853044b4a678d3f7ec72dcb61d22274'
            'e0872fab5f7b6b108dfef3e8879c08be417955082571a10152d03ccce296d2f7'
            '351fec847a3f6cd9b62f7930c09bb3eef46da2b350a3c733a766d11ff6126612'
            'b96739978479fe205601882d0bd9498502f8c40e934ca12f9c183e19832f99e7'
            '19ba93a502ff0c9db6e197f09eda9881999e4d80c264bc40d60f96e9423dd1a9'
            '064c7b70db071cd639355677fe1132875c8f82b32439c2a369159376bf98dbaf')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}"/fix-atom-sh.patch
  patch -Np1 -i "${srcdir}"/use-system-electron.patch
  patch -Np1 -i "${srcdir}"/use-system-apm.patch
  patch -Np1 -i "${srcdir}"/fix-license-path.patch

  # apm with system (updated) nodejs cannot 'require' modules inside asar
  sed -e "s/, 'generate-asar'//" -i build/Gruntfile.coffee

  # Fix for Electron 1.0.0
  patch -Np1 -i "${srcdir}"/deprecated-api.patch
  sed -e 's/"settings-view": "0.235.1"/"settings-view": "0.236.0"/' \
      -i package.json

  # Fix for Electron 1.2.0
  patch -Np1 -i "${srcdir}"/beforeunload.patch
  patch -Np1 -i "${srcdir}"/run-as-node.patch

  # Fix for Electron 1.3.0
  #
  # NodeGit fails to compile with Electron 1.3.0
  # (https://github.com/nodegit/nodegit/issues/1089)
  # so we disable it, as done here https://github.com/atom/atom/pull/12167
  patch -Np1 -i "${srcdir}"/stop-using-nodegit.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export ATOM_RESOURCE_PATH="$srcdir/atom-$pkgver"
  # If unset, ~/.atom/.node-gyp/.atom/.npm is used
  export NPM_CONFIG_CACHE="${HOME}/.atom/.npm"
  apm clean
  apm install

  # Fix for Node 6
  cd node_modules/tree-view
  patch -Np1 -i "${srcdir}"/tree-view.patch
  cd ../..

  # Fix for Electron 1.3.0
  _node_gyp="node-gyp rebuild --target=$(</usr/lib/electron/version) --dist-url=https://atom.io/download/atom-shell"
  cd node_modules/marker-index
  patch -Np1 -i "${srcdir}"/fix-marker-index.patch
  ${_node_gyp}
  cd ../oniguruma
  patch -Np1 -i "${srcdir}"/fix-oniguruma.patch
  ${_node_gyp}
  cd ../../

  _packagesToDedupe=('abbrev'
                     'amdefine'
                     'atom-space-pen-views'
                     'cheerio'
                     'domelementtype'
                     'fs-plus'
                     'grim'
                     'highlights'
                     'humanize-plus'
                     'iconv-lite'
                     'inherits'
                     'loophole'
                     'oniguruma'
                     'q'
                     'request'
                     'rimraf'
                     'roaster'
                     'season'
                     'sigmund'
                     'semver'
                     'through'
                     'temp')
  apm dedupe ${_packagesToDedupe[@]}
  cd build
  npm install
  cd ..
  script/grunt --channel=stable
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -d -m 755 "${pkgdir}"/usr/lib
  cp -r out/Atom/resources/app "${pkgdir}"/usr/lib
  mv "${pkgdir}"/usr/lib/app "${pkgdir}"/usr/lib/atom

  install -d -m 755 "${pkgdir}/usr/share/applications"
  sed -e "s|<%= appName %>|Atom|" \
      -e "s/<%= description %>/${pkgdesc}/" \
      -e "s|<%= installDir %>/share/<%= appFileName %>/atom|electron --app=/usr/lib/atom|" \
      -e "s|<%= iconPath %>|/usr/lib/atom/resources/atom.png|" \
      resources/linux/atom.desktop.in > "${pkgdir}/usr/share/applications/atom.desktop"

  install -D -m 755 out/Atom/resources/new-app/atom.sh "${pkgdir}/usr/bin/atom"

  install -D -m 644 out/Atom/resources/LICENSE.md \
          "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"

  # Remove useless stuff
  find "${pkgdir}"/usr/lib/atom/node_modules \
      -name "*.a" -exec rm '{}' \; \
      -or -name "*.bat" -exec rm '{}' \; \
      -or -name "benchmark" -prune -exec rm -r '{}' \; \
      -or -name "doc" -prune -exec rm -r '{}' \; \
      -or -name "html" -prune -exec rm -r '{}' \; \
      -or -name "man" -prune -exec rm -r '{}' \; \
      -or -path "*/less/gradle" -prune -exec rm -r '{}' \; \
      -or -path "*/task-lists/src" -prune -exec rm -r '{}' \;
}
